buildscript {
  repositories {
    jcenter()
    maven { url = "https://files.minecraftforge.net/maven" }
  }
  dependencies {
    classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.+", changing: true
  }
}

apply plugin: "checkstyle"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "pmd"

// Mod information.
version = "1.16.5-9.0.0"
group = "com.judge40.minecraft.bettermobgriefinggamerule"
archivesBaseName = "bettermobgriefinggamerule"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
  // MCP mappings version.
  mappings channel: "official", version: "1.16.5"

  // Default run configurations.
  runs {
    client {
      workingDirectory project.file("run")

      // Recommended logging data for a userdev environment.
      property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

      // Recommended logging level for the console.
      property "forge.logging.console.level", "debug"

      mods {
        bettermobgriefinggamerule {
          source sourceSets.main
        }
      }
    }

    server {
      workingDirectory project.file("run")

      // Recommended logging data for a userdev environment.
      property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

      // Recommended logging level for the console.
      property "forge.logging.console.level", "debug"

      mods {
        bettermobgriefinggamerule {
          source sourceSets.main
        }
      }
    }

    data {
      workingDirectory project.file("run")

      // Recommended logging data for a userdev environment.
      property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

      // Recommended logging level for the console.
      property "forge.logging.console.level", "debug"

      args "--mod", "bettermobgriefinggamerule", "--all", "--output", file("src/generated/resources/")

      mods {
        bettermobgriefinggamerule {
          source sourceSets.main
        }
      }
    }
  }
}

repositories {
  jcenter()
}

dependencies {
  minecraft "net.minecraftforge:forge:1.16.5-36.1.0"

  // Test dependencies.
  ext.junitVersion = "5.6.2"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

  testImplementation("junit:junit:4.13")
  testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.6.2")

  testImplementation "org.mockito:mockito-inline:3.4.4"
}

checkstyle {
  toolVersion = "8.34"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jar.finalizedBy("reobfJar")

test {
  useJUnitPlatform()
}

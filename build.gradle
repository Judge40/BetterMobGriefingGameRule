buildscript {
  repositories {
    jcenter()
    maven {
      name = "forge"
      url = "http://files.minecraftforge.net/maven"
    }
  }
  dependencies {
    classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
  }
}

apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "pmd"

// Mod information.
version = "1.9.4-3.0.0"
group= "com.judge40.minecraft.bettermobgriefinggamerule"
archivesBaseName = "bettermobgriefinggamerule"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
  // MinecraftForge version.
  version = "1.9.4-12.17.0.1976"

  // MCP mappings version.
  mappings = "stable_26"

  // Use shared run directory if available.
  if (file("../run").exists()) {
    runDir = "../run"
  } else {
    runDir = "run"
  }
}

dependencies {
  // JMockit dependency.
  testCompile "org.jmockit:jmockit:1.34"
  // JUnit dependency.
  testCompile "junit:junit:4.12"
}

findbugs {
  effort = "max"
  reportLevel = "low"
}

findbugsMain {
  excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilterMain.xml")
}

findbugsTest {
  excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilterTest.xml")
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

jar {
  manifest {
    attributes "FMLCorePlugin":"com.judge40.minecraft.bettermobgriefinggamerule.asm.LoadingPlugin",
        "FMLCorePluginContainsFMLMod":"true"
  }
}

processResources {
  // This will ensure that this task is redone when the versions change.
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version

  // Replace stuff in mcmod.info, nothing else.
  from(sourceSets.main.resources.srcDirs) {
    include "mcmod.info"

    // Replace version and mcversion.
    expand "version":project.version,"mcversion":project.minecraft.version
  }

  // Copy everything else, thats not the mcmod.info.
  from(sourceSets.main.resources.srcDirs) {
    exclude "mcmod.info"
  }
}

test {
  systemProperties["jmockit-coverage-metrics"] = "all"
  systemProperties["jmockit-coverage-outputDir"] = "build/reports/coverage"
}
